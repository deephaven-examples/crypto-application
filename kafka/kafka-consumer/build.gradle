import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'io.deephaven.crypto.java-library-conventions'
    id 'com.bmuschko.docker-remote-api' version '7.1.0'
}

dependencies {
    api project(':kafka-common')

    // To be provided by the DH environment
    compileOnly 'io.deephaven:deephaven-table-api:0.5.0'
    compileOnly 'io.deephaven:deephaven-qst:0.5.0'
}

task prepareDocker(type: Sync) {
    from 'src/main/docker/Dockerfile'
    from('src/main/app.d'){
        into 'app.d'
    }
    from(configurations.runtimeClasspath) {
        into 'libs'
    }
    from(jar) {
        into 'libs'
    }
    into 'build/docker'
}

task buildDocker(type: DockerBuildImage) {
    dependsOn prepareDocker
    images.add('deephaven/crypto-consumer:local-build')
}

assemble.dependsOn buildDocker
