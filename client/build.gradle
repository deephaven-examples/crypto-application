import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

plugins {
    id 'io.deephaven.crypto.java-application-conventions'
    id 'com.bmuschko.docker-java-application' version '7.1.0'
}

String deephavenCoreVersion = project.property('deephaven-core.version')
String base = project.property('client.base')
String image = project.property('client.image')

dependencies {
    implementation "io.deephaven:deephaven-client-flight-dagger:${deephavenCoreVersion}"

    implementation 'org.slf4j:slf4j-api:1.7.32'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.5'
}

application {
    mainClass = 'io.deephaven.crypto.client.Main'
    applicationDefaultJvmArgs = [ '-Xmx256m' ]
}

docker {
    javaApplication {
        baseImage = base
        images = [ image ]
        maintainer = project.property('maintainer')
        jvmArgs = [ '-Xmx256m' ]
    }
}

project.tasks.register('pushImage', DockerPushImage) {
    it.dependsOn dockerBuildImage
    it.inputs.files dockerBuildImage.outputs.files
    it.images.add(image)
}

assemble.dependsOn dockerBuildImage
